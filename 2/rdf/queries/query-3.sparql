BASE <http://mff.cuni.cz/NPRG036-T2G4/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/> 
PREFIX dbpedia: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 

# For every combination of genre and language, list the amount of books
# as well as authors who have written such books.
# (Includes translations.)
# (IRIs also listed to avoid ambiguities.)

SELECT ?genreName ?languageName (COUNT(?book) as ?bookCount) (COUNT(DISTINCT ?author) as ?authorCount) ?genre ?language
WHERE {
    ?genre a dbpedia:LiteraryGenre ;
        rdfs:label ?genreName .
    ?language a schema:Language ;
        schema:name ?languageName .
    ?book schema:genre ?genre ;
        schema:inLanguage ?language ;
        schema:author ?author .
}
GROUP BY ?genreName ?languageName ?genre ?language