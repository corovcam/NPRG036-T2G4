BASE <http://mff.cuni.cz/NPRG036-T2G4/> 
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX schema: <http://schema.org/> 
PREFIX dbpedia: <http://dbpedia.org/ontology/> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# Count different languages in which each library currently offers books,
# list out their names and URLs along with the count, libraries with highest counts first.
# (IRIs also listed to avoid ambiguities.)

SELECT ?libraryName ?libraryUrl (COUNT(DISTINCT ?language) AS ?languageCount) ?library
WHERE {
    ?library a schema:Library ; 
        schema:makesOffer/schema:itemOffered/schema:inLanguage ?language ;
        schema:name ?libraryName ;
        schema:url ?libraryUrl .
}
GROUP BY ?libraryName ?libraryUrl ?library
ORDER BY DESC(?languageCount)